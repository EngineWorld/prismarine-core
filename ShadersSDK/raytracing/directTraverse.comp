#version 460 core

#include "../include/constants.glsl"
#include "../include/structs.glsl"
#include "../include/uniforms.glsl"
#include "../include/rayslib.glsl"
#include "../include/morton.glsl"
#include "../include/mathlib.glsl"
#include "../include/vertex.glsl"

layout ( std430, binding = 9 ) readonly buffer NodesBlock { HlbvhNode Nodes[]; };
layout ( std430, binding = 17 ) readonly buffer LeafBlock { HlbvhNode Leafs[]; };
layout ( std430, binding = 18 ) readonly buffer MortonIndicesBlock { int MortonIndices[]; };

struct SharedVarsData {
    int bakedStackCount;
    int chainID;
    int prevHt;
};

struct TResult {
    float dist;
    int triangleID;
    int materialID; // legacy
    float predist;
    vec4 uv;
    vec4 normal;
};

uint L = 0;
int deferredPtr = 0;
SharedVarsData sharedVarsData;

const int STACK_SIZE = 16;
const int bakedFragments = 8;

shared int deferredStack[WORK_SIZE][STACK_SIZE];
shared vec4 bakedStack[WORK_SIZE][bakedFragments];

void reorderTriangles() {
    sharedVarsData.bakedStackCount = min(sharedVarsData.bakedStackCount, bakedFragments);

    for (int iround = 1; iround < sharedVarsData.bakedStackCount; iround++) {
        for (int index = 0; index < sharedVarsData.bakedStackCount - iround; index++) {
            
            // choice from both
            vec4 bakedPair[2] = {bakedStack[L][index], bakedStack[L][index+1]}; // cache
            bool lessIdx = floatBitsToInt(bakedPair[0].w) <= floatBitsToInt(bakedPair[1].w);
            bool deeper = lessF(bakedPair[0].z, bakedPair[1].z);

            // swap or not
            bool swapable = (lessIdx || deeper);
            ivec2 pairIdx = swapable ? ivec2(1, 0) : ivec2(0, 1);
            if (swapable) {
                bakedStack[L][index  ] = bakedPair[pairIdx.x];
                bakedStack[L][index+1] = bakedPair[pairIdx.y];
            }
        }
    }

    // initial clean list
    int cleanBakedStackCount = 0;

    // select only unique triangle ID's
    for (int iround = 0; iround < bakedFragments; iround++) {
        if (iround >= sharedVarsData.bakedStackCount-1) break;
        if (floatBitsToInt(bakedStack[L][iround+1].w) != floatBitsToInt(bakedStack[L][iround].w)) {
            bakedStack[L][cleanBakedStackCount++] = bakedStack[L][iround];
        }
    }

    // copy last primitive
    if (sharedVarsData.bakedStackCount > 0 && cleanBakedStackCount <= bakedFragments) {
        bakedStack[L][cleanBakedStackCount++] = bakedStack[L][sharedVarsData.bakedStackCount-1];
    }
    
    sharedVarsData.bakedStackCount = cleanBakedStackCount;
}



HitRework interpolateMeshData(inout HitRework res) {
    int tri = floatBitsToInt(res.uvt.w);

    bool validInterpolant = greaterEqualF(res.uvt.z, 0.0f) && lessF(res.uvt.z, INFINITY) && tri != LONGEST;
    if (anyInvocation(validInterpolant)) {
        vec3 trinorms[3];
        vec3 triverts[3];
        vec4 texcoords[3];
        vec4 colors[3];
        vec4 mods[3];

        for (int x=0;x<3;x++) {
            ivec2 mos = gatherMosaic(getUniformCoord(tri));
            triverts[x] = fetchMosaic(vertex_texture, mos, x).xyz;
            trinorms[x] = fetchMosaic(normal_texture, mos, x).xyz;
            texcoords[x] = fetchMosaic(texcoords_texture, mos, x);
            colors[x] = vec4(1.0f);
            mods[x] = fetchMosaic(modifiers_texture, mos, x);
        }

        vec3 deltaPos1 = triverts[1] - triverts[0];
        vec3 deltaPos2 = triverts[2] - triverts[0];
        vec2 uv = res.uvt.xy;
        vec3 nor = normalize(cross(deltaPos1, deltaPos2));
        vec3 normal = fma(trinorms[0], vec3(1.0f - uv.x - uv.y), fma(trinorms[1], vec3(uv.x), trinorms[2] * vec3(uv.y)));
        normal = lessF(length(normal), 0.f) ? nor : normalize(normal);
        normal = normal * sign(dot(normal, nor));

        bool delta = all(equal(texcoords[0].xy, texcoords[1].xy)) && all(equal(texcoords[0].xy, texcoords[2].xy));
        vec2 deltaUV1 = delta ? vec2(1.0f, 0.0f) : texcoords[1].xy - texcoords[0].xy;
        vec2 deltaUV2 = delta ? vec2(0.0f, 1.0f) : texcoords[2].xy - texcoords[0].xy;

        float f = 1.0f / fma(deltaUV1.x, deltaUV2.y, -deltaUV1.y * deltaUV2.x);
        vec3 tan = fma(deltaPos1, vec3(deltaUV2.y), -deltaPos2 * deltaUV1.y) * f;

        if (validInterpolant) {
            res.normalHeight = vec4(normal, 0.0f);
            res.tangent = vec4(normalize(tan - normal * sign(dot(tan, nor))), 0.0f);
            res.texcoord.x = packHalf2x16(fma(texcoords[0].xy, vec2(1.0f - uv.x - uv.y), fma(texcoords[1].xy, vec2(uv.x), texcoords[2].xy * vec2(uv.y))));
            res.texcoord.y = packHalf2x16(fma(texcoords[0].zw, vec2(1.0f - uv.x - uv.y), fma(texcoords[1].zw, vec2(uv.x), texcoords[2].zw * vec2(uv.y))));
            res.albedo = fma(colors[0], vec4(1.0f - uv.x - uv.y), fma(colors[1], vec4(uv.x), colors[2] * vec4(uv.y)));
            res.materialID = mats[tri];
        }
    }

    return res;
}

void includeChain(in int chainID, in vec4 intersection){
    int hid = atomicIncHt(true);
    HitRework hitp = hitBuf.nodes[hid];
    hitp.uvt = intersection;

    hitp.next = -1;
    hitBuf.nodes[hid] = interpolateMeshData(hitp);
    if (sharedVarsData.prevHt == -1) {
        sharedVarsData.chainID = hid;
    } else {
        hitBuf.nodes[sharedVarsData.prevHt].next = hid;
    }
    sharedVarsData.prevHt = hid;
}

TResult choiceBaked(inout TResult res, in vec3 orig, in vec3 dir, in int tpi) {
    reorderTriangles();
    for (int i=0;i<sharedVarsData.bakedStackCount;i++) {
        includeChain(i, bakedStack[L][i]);
    }
    return res;
}

TResult testIntersection(inout TResult res, in vec3 orig, in vec3 dir, in int tri, in bool isValid) {
    bool validTriangle = 
        isValid && 
        tri >= 0 && 
        tri != LONGEST && 
        tri != res.triangleID;

    if (anyInvocation(validTriangle)) {
        vec2 uv = vec2(0.0f);
        float _d = intersectTriangle(orig, dir, tri, uv, validTriangle);
        bool near = validTriangle && lessF(_d, INFINITY) && lessEqualF(_d, res.predist) && greaterEqualF(_d, 0.0f);

        if (near) {
            if (!equalF(_d, res.predist)) { // if distance not equal, reset baked storage counting
                sharedVarsData.bakedStackCount = 0;
            }
            res.predist = _d;
            res.triangleID = tri;
            bakedStack[L][add(sharedVarsData.bakedStackCount, 1)] = vec4(uv, _d, intBitsToFloat(tri));
        }
    }

    return res;
}

const int groupSize = 1;
void includeLeaf(in vec3 origin, in vec3 direct, in bool validBox, in HlbvhNode node, in vec3 boxHit, inout TResult lastRes){
    bool isLeaf = validBox;
    if (anyInvocation(isLeaf)) {
        for (int ls=0;ls<groupSize;ls+=1) {
            bool overtaken = ls > node.pdata.y-node.pdata.w || !isLeaf;
            if (anyInvocation(!overtaken)) {
                int triIdx = overtaken ? LONGEST : Leafs[MortonIndices[node.pdata.w + ls]].pdata.w;
                testIntersection(lastRes, origin.xyz, direct.xyz, triIdx, isLeaf && !overtaken);
            }
        }
    }
}

vec3 projectVoxels(in vec3 orig) {
    return mult4(vec4(orig, 1.0f), GEOMETRY_BLOCK geometryUniform.transform).xyz;
}

vec3 unprojectVoxels(in vec3 orig) {
    return mult4(vec4(orig, 1.0f), GEOMETRY_BLOCK geometryUniform.transformInv).xyz;
}



vec3 bboxNormal(in bbox box, in vec3 hit){
    vec3 c = (box.mn + box.mx).xyz * 0.5f;
    vec3 p = hit - c;
    vec3 d = abs((box.mx - box.mn).xyz * 0.5f);
    float bias = 1.000001f;
    return normalize(floor(p / d * bias));
}


TResult traverse(in vec3 origin, in vec3 direct, in bool valid) {
    TResult lastRes;
    lastRes.dist = sharedVarsData.prevHt != -1 ? hitBuf.nodes[sharedVarsData.prevHt].uvt.z : INFINITY;
    lastRes.predist = sharedVarsData.prevHt != -1 ? hitBuf.nodes[sharedVarsData.prevHt].uvt.z : INFINITY;
    lastRes.triangleID = LONGEST;
    lastRes.materialID = LONGEST;
    lastRes.normal = vec4(0.f);

    // deferred stack
    deferredPtr = 0;
    deferredStack[L][0] = -1;
    sharedVarsData.chainID = -1;
    sharedVarsData.bakedStackCount = 0;
    for (int i=0;i<bakedFragments;i++) {bakedStack[L][i] = vec4(vec2(0.0f), INFINITY, LONGEST);}

    // something fix
    direct = normalize(direct);

    // test constants
    vec3 torig = projectVoxels(origin);
    //vec3 tdirproj = mult4(GEOMETRY_BLOCK geometryUniform.transform, vec4(direct, 1.0)).xyz;
    vec3 tdirproj = mult4(vec4(direct, 0.0), GEOMETRY_BLOCK geometryUniform.transform).xyz;
    float dirlen = length(tdirproj) / max(length(direct), 0.000001f);
    float dirlenInv = 1.f / max(dirlen, 0.000001f);
    vec3 dirproj = normalize(tdirproj);

    // test with root node
    float near = INFINITY, far = INFINITY;
    float d = intersectCubeSingle(torig, dirproj, vec4(vec3(0.f), 1.0f), vec4(vec3(1.f), 1.0f), near, far);
    //lastRes.predist = min(lastRes.predist, far * dirlenInv);

    // init state
    int idx = 0;
    bool validBox = valid && lessF(d, INFINITY) && lessF(d * dirlenInv, INFINITY) && greaterEqualF(d, 0.0f);
    HlbvhNode node = Nodes[idx];
    for (int i=0;i<8192;i++) {
        if (allInvocations(!validBox)) break;

        // choice near box
        bool skip = false;
        bool notLeaf = node.pdata.x != node.pdata.y && validBox;
        if (anyInvocation(notLeaf)) {
            HlbvhNode lnode = Nodes[node.pdata.x];
            HlbvhNode rnode = Nodes[node.pdata.y];

            vec2 inf2 = vec2(INFINITY), nears = inf2, fars = inf2;
            vec2 hits = vec2(
                intersectCubeSingle(torig, dirproj, lnode.box.mn, lnode.box.mx, nears.x, fars.x),
                intersectCubeSingle(torig, dirproj, rnode.box.mn, rnode.box.mx, nears.y, fars.y)
            );

            int overlapsGeneral = (notLeaf ? 3 : 0) & 
                cB2(lessThanEqual(hits, vec2(INFINITY-PZERO))) & 
                cB2(lessThanEqual(hits * dirlenInv, vec2(INFINITY-PZERO))) & 
                cB2(greaterThan(hits, -vec2(PZERO))) & 
                cB2(lessThanEqual(nears, vec2(lastRes.predist+PZERO) * dirlen)) & 
                cB2(bvec2(node.pdata.x != -1, node.pdata.y != -1)) & 
                cB2(bvec2(node.pdata.x != node.pdata.y));
            int overlaps = overlapsGeneral & cB2(bvec2(lnode.pdata.x != lnode.pdata.y, rnode.pdata.x != rnode.pdata.y));

            bool anyOverlap = anyB(overlaps);
            if (anyInvocation(anyOverlap)) {
                bvec2 overlapsBool = cI2(overlaps);
                bool leftOrder = allB2(overlaps) ? lessEqualF(nears.x, nears.y) : overlapsBool.x;
                ivec2 leftright = mix(ivec2(-1), node.pdata.xy, overlapsBool);
                leftright = leftOrder ? leftright : leftright.yx;

                if (anyOverlap) {
                    if (deferredPtr < STACK_SIZE && leftright.y != -1 && leftright.x != leftright.y) {
                        deferredStack[L][deferredPtr++] = leftright.y;
                    }
                    idx = leftright.x;
                    node = Nodes[idx]; // for optimized read-write operations
                }
            }

            // because traverse stack is limited, we does not including leafs to this stack
            overlapsGeneral &= cB2(bvec2(lnode.pdata.x == lnode.pdata.y, rnode.pdata.x == rnode.pdata.y)); // is leafs
            bool anyLeaf = anyB(overlapsGeneral); skip = skip || anyLeaf; // if any leaf found, skipping
            if (anyInvocation(anyLeaf)) {
                bvec2 overlaps = cI2(overlapsGeneral);
                bool leafOrder = allB2(overlapsGeneral) ? lessEqualF(nears.x, nears.y) : overlaps.x;
                overlaps = leafOrder ? overlaps : overlaps.yx;
                nears = leafOrder ? nears : nears.yx;
                hits = leafOrder ? hits : hits.yx;
                includeLeaf(origin, direct, validBox && overlaps.x && lessEqualF(nears.x, lastRes.predist), leafOrder ? lnode : rnode, dirproj * hits.x + torig, lastRes);
                includeLeaf(origin, direct, validBox && overlaps.y && lessEqualF(nears.y, lastRes.predist), leafOrder ? rnode : lnode, dirproj * hits.y + torig, lastRes);
                skip = false; // continue search when found
            }

            skip = skip || anyOverlap; // have any branching? don't upstream to next or parents
        }

        // go to parent 
        if (!skip) {
            int ptr = --deferredPtr;
            bool valid = ptr >= 0; 
            idx = valid ? atomicExchange(deferredStack[L][ptr], -1) : -1;
            node = Nodes[idx]; // for optimized read-write operations
            validBox = validBox && valid && idx >= 0;
        };
    }

    lastRes.triangleID = LONGEST;
    choiceBaked(lastRes, origin.xyz, direct.xyz, 0);
    return lastRes;
}

LOCAL_SIZE_LAYOUT;

void main() {
    L = gl_LocalInvocationID.x;

    int it = int(gl_GlobalInvocationID.x);
    bool overflow = it >= RAY_BLOCK samplerUniform.rayCount;
    if (allInvocations(overflow)) return;

    int t = activedBuf.indc[it];
    RayRework ray = rayBuf.nodes[t];
    overflow = RayActived(ray) == 0 || overflow;
    if (allInvocations(overflow)) return;

    sharedVarsData.prevHt = ray.hit;
    traverse(ray.origin.xyz, ray.direct.xyz, !overflow);
    ray.hit = sharedVarsData.chainID;
    if (!overflow) rayBuf.nodes[t] = ray; // avoid invalids
}
