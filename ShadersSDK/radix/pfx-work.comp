#version 450 core

// Readme license https://github.com/AwokenGraphics/prismarine-core/blob/master/LICENSE.md

#include "./includes.glsl"

#define WG_COUNT 32

shared uint localHistogram[RADICES];
void prefixScan(){
    UVEC_WARP localIdx = LT_IDX;
    memoryBarrier(); barrier();
    uint prv = localIdx >= 1 ? localIdx-1 : 0;
    uint tmp = localIdx >= 1 && localIdx < RADICES ? localHistogram[prv] : 0;
    memoryBarrier(); barrier();
    if (localIdx < RADICES) localHistogram[localIdx] = tmp;

    // reduction prefix-scan
    for (int i=1;i<RADICES;i<<=1) {
        memoryBarrier(); barrier();
        uint idx = localIdx;
        uint prv = idx >= i ? idx-i : 0;
        uint off = idx >= i && idx < RADICES ? localHistogram[prv] : 0;
        uint hst = idx < RADICES ? localHistogram[idx] : 0;
        memoryBarrier(); barrier();
        if (idx < RADICES) localHistogram[idx] = off + hst;
    }
    memoryBarrier(); barrier();
}

layout (local_size_x = RADICES) in;

shared uint seed[RADICES];
void main() {
    LT_IDX = gl_LocalInvocationID.x;
    LC_IDX = (gl_LocalInvocationID.x / gl_SubGroupSizeARB);
    LANE_IDX = (gl_LocalInvocationID.x % gl_SubGroupSizeARB);

    if (LT_IDX < RADICES) {
        localHistogram[LT_IDX] = 0;
    }
    memoryBarrier(); barrier();

    // prefix scans of workers for every radice
    uint prv = LT_IDX;
    for (uint wk=0;wk<WG_COUNT;wk++) {
        uint idx = LT_IDX + wk * RADICES;
        uint pfx = LT_IDX < RADICES && wk > 0 ? Histogram[prv] : 0;
        uint val = LT_IDX < RADICES && wk > 0 ? PrefixSum[prv] : 0;
        if (LT_IDX < RADICES) Histogram[idx] = val + pfx;
        prv = idx;
    }
    memoryBarrier(); barrier();

    uint head = LT_IDX + (WG_COUNT-1) * RADICES;
    if (LT_IDX < RADICES) {
        localHistogram[LT_IDX] = Histogram[head] + PrefixSum[head];
    }

    // prefix scan of seeds
    prefixScan();

    // apply prefix scans
    for (uint wk=0;wk<WG_COUNT;wk++) {
        uint idx = LT_IDX + wk * RADICES;
        if (LT_IDX < RADICES) {
            PrefixSum[idx] = Histogram[idx] + localHistogram[LT_IDX];
        }
    }
}
