#version 450

#include "../include/constants.glsl"
#include "../include/structs.glsl"
#include "../include/uniforms.glsl"
#include "./includes.glsl"

#define LEAFIDX(i) (Range.y + i)
#define NODEIDX(i) (clamp(i, 1, Range.y-1))

layout ( local_size_x = 1024 ) in;

int nlz (in uint x) {
    return 31-findMSB(x);
}

int nlz (in int x) {
    return 31-findMSB(uint(x));
}

int findSplit( int first, int last)
{
    const uint firstCode = Mortoncodes[first];
    const uint lastCode = Mortoncodes[last];
    if (firstCode == lastCode) {
        return (first + last) >> 1;
    }

    const int commonPrefix = nlz(firstCode ^ lastCode);
    int split = first;
    int step = last - first;
    do {
        step = (step + 1) >> 1;
        const int newSplit = split + step;
        if (newSplit < last) {
            const uint splitCode = Mortoncodes[newSplit];
            const int splitPrefix = nlz(firstCode ^ splitCode);
            if (splitPrefix > commonPrefix) {
                split = newSplit;
            }
        }
    } while (step > 1);

    return clamp(split, 0, Range.y-2);
}

shared int lRange[2];
shared int lCounter;

void main() {
    if (gl_WorkGroupID.x > 0) return; // not supported

    const uint threadID = uint(gl_LocalInvocationID.x);

    if (threadID == 0) {
        lCounter = 1;
        lRange[0] = 0;
        lRange[1] = 1;

        HlbvhNode node = Nodes[threadID];
        node.box.mn = vec4(0.0f);
        node.box.mx = vec4(0.0f);
        node.range = ivec2(0, Range.y-1);
        node.parent = -1;
        node.triangle = -1;
        
        Nodes[threadID] = node;
        Flags[threadID] = 0;
    }
    
    barrier(); memoryBarrier();

    for (int h=0;h<256;h++) {
        const uint workSize = (lRange[1] <= lRange[0]) ? 0 : (((lRange[1] - lRange[0]) - 1) / gl_WorkGroupSize.x + 1);
        for (int i=0;i<workSize;i++) {
            
            const uint sWorkID = gl_WorkGroupSize.x * i;
            const uint prID = lRange[0] + (sWorkID + threadID);

            if (prID < lRange[1]) {
                HlbvhNode parentNode = Nodes[prID];

                if (parentNode.range.x != parentNode.range.y) {
                    // find split
                    const int split = findSplit(parentNode.range.x, parentNode.range.y);

                    // add to list
                    const int hid = atomicAdd(lCounter, 2);
                    const ivec2 hid2 = ivec2(hid + 0, hid + 1);

                    HlbvhNode leftNode = Nodes[hid2.x];
                    leftNode.box.mn = vec4(0.0f);
                    leftNode.box.mx = vec4(0.0f);
                    leftNode.range = ivec2(parentNode.range.x, split+0);
                    leftNode.parent = int(prID);
                    leftNode.triangle = -1;
                    
                    HlbvhNode rightNode = Nodes[hid2.y];
                    rightNode.box.mn = vec4(0.0f);
                    rightNode.box.mx = vec4(0.0f);
                    rightNode.range = ivec2(split+1, parentNode.range.y);
                    rightNode.parent = int(prID);
                    rightNode.triangle = -1;
                    
                    // left node
                    Nodes[hid2.x] = leftNode;
                    Flags[hid2.x] = 0;

                    // right node
                    Nodes[hid2.y] = rightNode;
                    Flags[hid2.y] = 0;

                    // connect with childrens
                    parentNode.range = hid2;
                    Nodes[prID] = parentNode;
                } else {
                    uint leafID = parentNode.range.x;

                    // make leafs
                    Leaf ourLeaf = Leafs[leafID];
                    ourLeaf.parent = int(prID);
                    Leafs[leafID] = ourLeaf;

                    // load leaf data
                    parentNode.box.mn = ourLeaf.box.mn;
                    parentNode.box.mx = ourLeaf.box.mx;
                    parentNode.range = ourLeaf.range;
                    parentNode.parent = parentNode.parent;
                    parentNode.triangle = ourLeaf.triangle;
                    Nodes[prID] = parentNode;
                }
            }

            // barrier for every loop?
            //barrier(); memoryBarrier();
        }

        barrier(); memoryBarrier();
        if (threadID == 0) {
            lRange[0] = lRange[1];
            lRange[1] = lCounter;
        }
        barrier(); memoryBarrier();

        if (lRange[1] <= lRange[0]) {
            break;
        }
    }
}
